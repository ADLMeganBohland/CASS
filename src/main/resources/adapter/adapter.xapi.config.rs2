
configFilePath=#string(str="adapter.xapi.json");


postData=#fileFromDatastream(name="config");
postObject=postData.fileToString().toObject();

configObject=#if(operator=#if(operator=#cFileExists(path=configFilePath), operand="true",
	eq=#fileToString(obj=#cfileLoad(path=configFilePath)).toObject(),
	ne=#object(
		xapiEndpoint="",
		xapiHostName="",
		xapiAuth="",
		enabled=#false()
	)
);

configVal=configObject.getByParam(param="@key");
#configVal=configVal;




configSet=postObject.displayJson(_collapse="true").fileSave(path=configFilePath);

/adapter/xapi/config/set=configSet;



configGet = configObject.displayJson(_collapse="true");

/adapter/xapi/config/get=configGet;





xapiEndpoint=#configVal(key="xapiEndpoint");
#xapiEndpoint=xapiEndpoint;

xapiHostname=#configVal(key="xapiHostName");
#xapiHostname=xapiHostname;

//This is the same auth key available in the LRS Statement Viewer demonstration.
xapiAuth=#configVal(key="xapiAuth");
#xapiAuth=xapiAuth;

xapiAdapterEnabled=#configVal(key="enabled");

//If the adapter is enabled, we convert and store all XAPI conversions upon system startup. This is done every startup, as alignments may change.
//As such, eventual consistency, especially when a new alignment is introduced, is limited to after startup.
xapiImportAutoExecute=#if(
	operator=xapiAdapterEnabled,
	operand="true",
	eq=#xapiConvert()
);

//Please bind a cron task to this service.
/xapi/tick=xapiImportAutoExecute;

xapiImportAutoExecute=#add(a=#thisEndpoint(),b="xapi/tick").httpGet();

#xapiImportAutoExecute=xapiImportAutoExecute;

xapiAdapterEmail=#sendEmail(
	_from="",
	_to="",
	_subject="New User in CASS Adapter: $(user)",
	_smtpHost="",
	_smtpPort="",
	_smtpUser="",
	_smtpPass="",
	user="@user",
	ppk="@ppk",
	_template="A new identity has been ingested by the CASS Adapter. They have the following identity: $(user)
We have automatically associated a keypair with them. Please ensure they receive the following private key for their use:

$(ppk)

Thank you."	
);
#xapiAdapterEmail=xapiAdapterEmail;


