//Retrieve all mastery estimates for the given learner or delete/clear mastery.

mastery=#object(
	a=#if(
		operator="@framework",
		operand="",
		eq=#error(code="400",msg="Bad request. Please specify framework=<url> using URL Parameter.")
	),
	b=#if(
		operator="@userId",
		operand="",
		eq=#error(code="400",msg="Bad request. Please specify userId=<userId> using URL Parameter.")
	),
	z=#if(
		operator="@competency",
		operand="",
		ne=#if(
	        operator="@methodType",
	        operand="DELETE",
	        eq=#deleteMasteryForOneCompetency(
				competencyId="@competency",
				username="@userId"
	        ),
	        ne=#masteryForOneCompetency(
				frameworkId="@framework",
				competencyId="@competency",
				username="@userId"
			)
		),
		eq=#if(
	        operator="@methodType",
	        operand="DELETE",
	        eq=#deleteMasteryForAllCompetencies(
				username="@userId"
	        ),
	        ne=#masteryForAllCompetencies(
				frameworkId="@framework",
				username="@userId"
			)
		)
	)
).get(z="").call(
	competency=#if(
		operator="@endpoint",
		operand="",
		eq=#add(a="@endpoint",b="@competency"),
		ne="@competency"
	),
	framework=#if(
		operator="@endpoint",
		operand="",
		eq=#add(a="@endpoint",b="@framework"),
		ne="@framework"
	)
);

/learner/mastery=mastery;

goal=#object(
	b=#if(
		operator="@userId",
		operand="",
		eq=#error(code="400",msg="Bad request. Please specify user=<user> using URL Parameter.")
	),
	z=#if(
		operator="@competency",
		operand="",
		ne=#if(
	        operator="@methodType",
	        operand="DELETE",
	        eq=#deleteGoalForOneCompetency(
				competencyId="@competency",
				username="@userId"
	        ),
	        ne=#if(
		        operator="@methodType",
		        operand="PUT",
		        eq=#setGoalForOneCompetency(
					competencyId="@competency",
					username="@userId"
		        ),
		        ne=#getGoalForOneCompetency(
					competencyId="@competency",
					username="@userId"
				)
			),
		),
		eq=#if(
	        operator="@methodType",
	        operand="DELETE",
	        eq=#deleteGoalsForAllCompetencies(
				username="@userId"
	        ),
	        ne=#goalsForAllCompetencies(
				username="@userId"
			)
		)
	)
).get(z="").call(
	competency=#if(
		operator="@endpoint",
		operand="",
		eq=#add(a="@endpoint",b="@competency"),
		ne="@competency"
	),
	framework=#if(
		operator="@endpoint",
		operand="",
		eq=#add(a="@endpoint",b="@framework"),
		ne="@framework"
	)
);

/learner/goal=goal;

contribute=#object(
	b=#if(
		operator="@userId",
		operand="",
		eq=#error(code="400",msg="Bad request. Please specify user=<user> using URL Parameter.")
	),
	c=#if(
		operator="@confidence",
		operand="",
		eq=#error(code="400",msg="Bad request. Please specify confidence=[0,1] using URL Parameter.")
	),
	z=#if(
		operator="@competency",
		operand="",
		ne=#contributeToMastery(
			frameworkId="@framework",
			competencyId="@competency",
			username="@userId",
			confidence="@confidence"
		)
	)
).get(z="").call(
	competency=#if(
		operator="@endpoint",
		operand="",
		eq=#add(a="@endpoint",b="@competency"),
		ne="@competency"
	),
	framework=#if(
		operator="@endpoint",
		operand="",
		eq=#add(a="@endpoint",b="@framework"),
		ne="@framework"
	)
);

/learner/mastery/contribute=contribute;