//Retrieve all competency estimates for the given learner or delete/clear competency.

competency=#object(
	b=#if(
		operator="@userId",
		operand="",
		eq=#error(code="500",msg="Bad request. Please specify userId=<userId> using URL Parameter.")
	),
	z=#if(
		operator="@competency",
		operand="",
		ne=#if(
	        operator="@methodType",
	        operand="POST",
	        eq=#createAssertionForCompetency(
				competencyId="@competency",
				username="@userId",
				confidence="@confidence"
			),
	        ne=#if(
		        operator="@methodType",
		        operand="DELETE",
		        eq=#deleteAssertionsForOneCompetency(
					competencyId="@competency",
					username="@userId"
		        ),
		        ne=#if(
					operator="@framework",
					operand="",
					eq=#error(code="500",msg="Bad request. Please specify framework=<url> using URL Parameter."),
					ne=#determineCompetenceForOneCompetency(
						frameworkId="@framework",
						competencyId="@competency",
						username="@userId"
					)
				)
			)
		),
		eq=#if(
	        operator="@methodType",
	        operand="DELETE",
	        eq=#deleteAssertionsForAllCompetencies(
				username="@userId"
	        ),
	        ne=#if(
				operator="@framework",
				operand="",
				eq=#error(code="500",msg="Bad request. Please specify framework=<url> using URL Parameter."),
				ne=#determineCompetenceForAllCompetencies(
					frameworkId="@framework",
					username="@userId"
				)
			)
		)
	)
).get(z=""
).call(
    competency=#if(
        operator="@competency",
        operand="",
        ne=#if(
            operator=#startsWith(obj="@competency",with="http"),
            operand="false",
            eq=#error(code="500",msg="The competency parameter does not begin with http. Please use the endpoint parameter or provide a fully qualified URL."),
            ne="@competency"
        )
    ),
    framework=#if(
        operator=#startsWith(obj="@framework",with="http"),
        operand="false",
        eq=#error(code="500",msg="The framework parameter does not begin with http. Please use the endpoint parameter or provide a fully qualified URL."),
        ne="@framework"
    ),
).call(
    a=#xapiTick(),
	competency=#if(operator="@competency",operand="",ne=#if(
		operator="@endpoint",
		operand="",
		ne=#add(_string="true",a="@endpoint",b="@competency"),
		eq="@competency"
	)),
	framework=#if(operator="@framework",operand="",ne=#if(
		operator="@endpoint",
		operand="",
		ne=#add(_string="true",a="@endpoint",b="@framework"),
		eq="@framework"
	))
).displayJson(_collapse="true");

/learner/competency=competency;

goal=#object(
        a=#beforeGet(),
	b=#if(
		operator="@userId",
		operand="",
		eq=#error(code="500",msg="Bad request. Please specify user=<user> using URL Parameter.")
	),
	z=#if(
		operator="@competency",
		operand="",
		ne=#if(
	        operator="@methodType",
	        operand="DELETE",
	        eq=#deleteGoalForOneCompetency(
				competencyId="@competency",
				username="@userId"
	        ),
	        ne=#if(
		        operator="@methodType",
		        operand="POST",
		        eq=#setGoalForOneCompetency(
					competencyId="@competency",
					username="@userId"
		        ),
		        ne=#getGoalForOneCompetency(
					competencyId="@competency",
					username="@userId"
				)
			),
		),
		eq=#if(
	        operator="@methodType",
	        operand="DELETE",
	        eq=#deleteGoalsForAllCompetencies(
				username="@userId"
	        ),
	        ne=#goalsForAllCompetencies(
				username="@userId"
			)
		)
	)
).get(z=""
).call(
	competency=#if(operator="@competency",operand="",ne=#if(
		operator="@endpoint",
		operand="",
		ne=#add(_string="true",a="@endpoint",b="@competency"),
		eq="@competency"
	)),
	framework=#if(operator="@framework",operand="",ne=#if(
		operator="@endpoint",
		operand="",
		ne=#add(_string="true",a="@endpoint",b="@framework"),
		eq="@framework"
	))
).displayJson(_collapse="true");

/learner/goal=goal;