//Copyright 2015 Eduworks Corporation and other contributing parties.
//Licensed under an Apache 2.0 License. See LICENSE in the root directory.
//This file is an RS2 file, a functional scripting language for defining web services.
//This code requires LEVR, a web service definition framework.

type=#add(a="@",b="type");
#type=type;
owner=#add(a="@",b="owner");
#owner=owner;
schema=#add(a="@",b="schema");
#schema=schema;
signatureParam=#add(a="@",b="signature");
#signature=signatureParam;

signatureArr=#toObject(obj="@signatureArr");
type=signatureArr.getByParam(param=#type());
owner=signatureArr.getByParam(param=#owner());
expiry=signatureArr.cget(expiry="");
signature=signatureArr.getByParam(param=signatureParam);

owner = owner.replace(
	replace="-----BEGIN PUBLIC KEY-----",
	with=""
).replace(
	replace="-----END PUBLIC KEY-----",
	with=""
).replace(
	regex="true",
	replace="\r?\n",
	with=""
);

signatureSheet=#fileFromDatastream(name="signatureSheet").fileToString().toArray();

verifySignature=#object(
	a=#if(
		operator=type,
		operand="",
		eq=#error(code="422",msg="Missing Signature Version.")
	),
	aa=#if(
		operator=type,
		operand="http://schema.eduworks.com/ebac/0.1/timeLimitedSignature",
		ne=#error(code="422",msg="Invalid Signature Version.")
	),
	b=#if(
		operator=expiry,
		operand="",
		eq=#error(code="422",msg="Missing expiry date.")
	),
	bb=#if(
		operator=expiry,
		operand=#date(_raw="true"),
		lt=#error(code="419",msg="A Signature is Expired.")
	),
	c=#if(
		operator=signature,
		operand="",
		eq=#error(code="496",msg="Missing Signature.")	
	),
	cc=#if(
		operator=signature.rsaVerify(
			pk=owner,
			against=signatureArr.removeByParam(param=signatureParam)
		),
		operand="true",
		ne=#error(code="451",msg=#add(a="Invalid Signature Detected: ",b=signature))
	),
	z=owner
).cget(z="");

signatureSheet=signatureSheet.cforEach(paramName="signatureArr",threaded="false",array="true",rethrow="true",op=verifySignature).cache(name="signatureSheet");
#signatureSheet=signatureSheet;